buildscript {
  apply from: 'gradle/dependencies.gradle'

  dependencies {
    classpath deps.androidPlugin
    classpath deps.kotlin.gradlePlugin
    classpath deps.spotlessPlugin
    classpath deps.kotlinterPlugin

    // Normally you would declare a version here, but we use dependency substitution in
    // settings.gradle to use the version built from inside the repo.
    classpath 'app.cash.molecule:molecule-gradle-plugin'
  }

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
}

allprojects {
  repositories {
    mavenCentral()
    google()
  }
}

subprojects {
  plugins.withId('java-base') {
    java {
      toolchain {
        languageVersion = JavaLanguageVersion.of(11)
      }
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = '11'
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
    def android = plugin.extension
    android.compileSdkVersion versions.compileSdk
    android.defaultConfig {
      minSdkVersion versions.minSdk
    }
    android.lintOptions {
      checkDependencies true
      checkReleaseBuilds false // Full lint runs as part of 'build' task.
    }
  }

  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      target("src/**/*.kt")
      licenseHeaderFile(rootProject.file('gradle/license-header.txt'))
    }
  }

  apply plugin: 'org.jmailen.kotlinter'
  kotlinter {
    disabledRules = [
      'filename',
    ]
  }
}
