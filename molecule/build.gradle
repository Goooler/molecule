buildscript {
  apply from: '../gradle/dependencies.gradle'

  dependencies {
    classpath deps.androidPlugin
    classpath deps.kotlin.gradlePlugin
    classpath 'com.vanniktech:gradle-maven-publish-plugin:0.18.0'
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:1.6.10'
    classpath deps.spotlessPlugin
    classpath deps.kotlinterPlugin
  }

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }
}

apply plugin: 'org.jetbrains.dokka'

allprojects {
  repositories {
    mavenCentral()
    google()
  }
}

subprojects {
  tasks.withType(Test).configureEach {
    testLogging {
      if (System.getenv("CI") == "true") {
        events = ["failed", "skipped", "passed"]
      }
      exceptionFormat "full"
    }
  }

  plugins.withId('java-base') {
    java {
      toolchain {
        languageVersion = JavaLanguageVersion.of(11)
      }
    }
  }

  tasks.withType(org.jetbrains.kotlin.gradle.dsl.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = '11'
    }
  }

  plugins.withType(com.android.build.gradle.BasePlugin).configureEach { plugin ->
    def android = plugin.extension
    android.compileSdkVersion versions.compileSdk
    android.defaultConfig {
      minSdkVersion versions.minSdk
    }
    android.lintOptions {
      checkDependencies true
      checkReleaseBuilds false // Full lint runs as part of 'build' task.
    }
  }

  apply plugin: 'com.diffplug.spotless'
  spotless {
    kotlin {
      target("src/**/*.kt")
      licenseHeaderFile(rootProject.file('../gradle/license-header.txt'))
    }
  }

  apply plugin: 'org.jmailen.kotlinter'
  kotlinter {
    disabledRules = [
      'filename',
    ]
  }

  plugins.withId('maven-publish') {
    publishing {
      repositories {
        maven {
          name = "installLocally"
          url = "${rootProject.buildDir}/localMaven"
        }
      }
    }
  }

  plugins.withId('signing') {
    signing {
      def signingKey = findProperty('signingKey')
      def signingPassword = ''
      useInMemoryPgpKeys(signingKey, signingPassword)
    }
  }
}
